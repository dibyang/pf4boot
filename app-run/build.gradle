
apply plugin: 'com.netflix.nebula.ospackage'
apply plugin: 'net.xdob.pf4boot'

build {
    enabled = false
}
jar {
    enabled = false
}

// 禁用所有以'publish'为前缀的任务
tasks.all { task ->
    if (task.name.startsWith('publish')) {
        task.enabled = false
    }
}

dependencies {
    runtimeOnly("ch.qos.logback:logback-classic:${logbak_version}")
    implementation project(":demo-app")
    plugin project(":plugin1")
    plugin project(":plugin2")
}

ext.packname='demo-app'

task prepackage(type: Copy)  {
    group 'build'
    copy
    {
        from("./package-scripts/"){
            include('*.sh')
            filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [app_name:packname])
        }
        into "$buildDir/ready"
    }
}

ospackage {
    packageName = "$packname"
    epoch= 1
    release = '1'
    os = LINUX
    user = 'root'
    conflicts("$packname")
    prepackage
    into "/$packname"

    from("bin"){
        into "bin"
        fileMode = 0555
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [app_name:packname])
    }

    from("demo-app.service"){
        into "/usr/lib/systemd/system/"
        rename('demo-app.service',"${packname}.service")
        fileMode = 0555
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [app_name:packname])
    }

    from("config"){
        fileType CONFIG | NOREPLACE
        into "config"
    }

    from(configurations.runtimeClasspath){
        into "lib"
        exclude '*-sources.jar','*-javadoc.jar','spring-boot-devtools*.jar'
    }

    from("plugins"){
        into "plugins"
        include "disabled.txt"
    }

    from(configurations.plugin){
        into "plugins"
        include "*.zip"
    }

}



buildRpm{
    group 'build'
    preInstall file("$buildDir/ready/preInstallCentos.sh")
    postInstall file("$buildDir/ready/postInstallCentos.sh")
    preUninstall file("$buildDir/ready/preUninstallCentos.sh")
}


task buildOSPacks(dependsOn:[buildRpm]){
    group 'build'
}